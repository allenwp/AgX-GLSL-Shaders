<< FunctionApproximations`

kNormLog2Min = -10;
kNormLog2Max = Rationalize[6.5];
kMidGray = Rationalize[0.18];
kLog2Min = Log2[(2^kNormLog2Min)*kMidGray];
kLog2Max = Log2[(2^kNormLog2Max)*kMidGray];
kLinearMin = (2^kNormLog2Min)*kMidGray;
kLinearMax = (2^kNormLog2Max)*kMidGray;
kPower = Rationalize[1.5];
kSlope = Rationalize[2.4];
kXPivot = Abs[kNormLog2Min/(kNormLog2Max - kNormLog2Min)];
kYPivot = kMidGray^(Rationalize[1.0/2.4]);

equationScale[transitionX_, transitionY_, power_, slope_] := 
  Module[{termA, termB}, 
   termA = (slope*(Rationalize[1 - transitionX]))^(Rationalize[-1*
        power]);
   termB = 
    SetPrecision[((slope*(Rationalize[1 - transitionX]))/(Rationalize[
           1 - transitionY]))^(power) - 1, 20];
   (termA*termB)^(Rationalize[-1/power])];

exponentialCurve[x_, scaleInput_, xPivot_, yPivot_, power_, 
  slope_] := (scaleInput*
    exponential[(slope*(x - xPivot))/scaleInput, power]) + yPivot

exponential[x_, power_] := x/((1 + (x^power))^(1/power))

calculateSigmoid[x_, xPivot_, yPivot_] := 
  Module[{scaleValue}, 
   scaleValue = 
    If[x < xPivot, -1*
      equationScale[1.0 - xPivot, 1.0 - yPivot, kPower, kSlope], 
     equationScale[xPivot, yPivot, kPower, kSlope]];
   exponentialCurve[x, scaleValue, xPivot, yPivot, kPower, kSlope]];

agxCurve[x_] := 
  Module[{normalizedX}, 
   normalizedX = (Log2[x/kMidGray] - kNormLog2Min)/(kNormLog2Max - 
       kNormLog2Min);
   calculateSigmoid[normalizedX, kXPivot, kYPivot]^kSlope];

Print["AgX tonemapping curve:"]
Plot[agxCurve[x], {x, kLinearMin, kLinearMax}, 
 ScalingFunctions -> {"Log2", None}]

Print["Part of Timothy Lottes' tonemapping equation for the toe:"]
Print["Generic soultion for c:"]
tonemapToe[x_, a_] := (x^a)/((x^a) + c);
Solve[{tonemapToe[midIn, a] == midOut}, {c}]

Print["Soultion for c with hardcoded mid grey:"]
Solve[{tonemapToe[kMidGray, a] == kMidGray}, {c}]

Print["NonlinearModelFit using solution for c from previous step. \
Range of toe only."]
dataToe = 
  Table[{x, agxCurve[x]}, {x, 
    2^Range[Log2[kLinearMin], Log2[kMidGray], 0.001]}];
nlmToe = 
  SetPrecision[
   NonlinearModelFit[
    dataToe, (x^a)/((x^a) + (41 9^(-1 + a) 50^-a)), {a}, x], 15];
nlmfToeResult = Normal[nlmToe]
Plot[nlmfToeResult, {x, kLinearMin, kLinearMax}, 
 ScalingFunctions -> {"Log2", None}]

(*Print["Slope \
calculations:"] nlmfResultPrime[x_]=D[nlm[x],x] Print["Slope at mid \
grey:"] nlmfResultPrime[kMidGray]*)

Print["Target slope at 0.18:"]
N[D[agxCurve[x], x] /. x -> kMidGray]

Print["Slope of flitted curve toe:"]
nlmToePrime[x_] = D[nlmToe[x], x]
nlmToePrime[kMidGray]

Print["Various constants:"]
Print["linear min:"]
N[kLinearMin, 20]
Print["linear max:"]
N[kLinearMax, 20]
Print["log2 min:"]
N[kLog2Min, 20]
Print["log2 max:"]
N[kLog2Max, 20]

Print["agxCurve min:"]
agxCurve[kLinearMin]
Print["agxCurve middle grey:"]
N[agxCurve[kMidGray]]
Print["agxCurve max:"]
agxCurve[kLinearMax]